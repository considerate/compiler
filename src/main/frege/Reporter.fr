module punkt0.Reporter where

import Data.Monoid(Semigroup,<>)
import frege.control.monad.trans.MonadTrans
import frege.control.monad.trans.MonadIO

data Accumulate e a = ALeft e | ARight a
derive Show (Accumulate e a)

toEither :: Accumulate e a -> (e | a)
toEither (ALeft e) = Left e
toEither (ARight a) = Right a

toAccumulate :: (e | a) -> Accumulate e a
toAccumulate (Left e) = ALeft e
toAccumulate (Right a) = ARight a

mapError :: (e -> f) -> Accumulate [e] a -> Accumulate [f] a
mapError f (ALeft es) = (ALeft (fmap f es))
mapError _ (ARight a) = (ARight a)

instance Functor (Accumulate e) where
    fmap f (ARight x) = ARight $ f x
    fmap _ (ALeft err) = ALeft err

instance Semigroup e => Applicative (Accumulate e) where
    pure = ARight
    ARight f <*> ARight x = ARight $ f x
    -- accumulate errors
    ALeft e <*> ALeft e' = ALeft (e <> e')
    ALeft e <*> _ = ALeft e
    _ <*> ALeft e = ALeft e

data ReporterT e m a = ReporterT {!run :: m (Accumulate e a)}

instance Functor f => Functor (ReporterT e f) where
    fmap f (ReporterT m) = ReporterT (fmap (fmap f) m)

instance (Applicative f, Semigroup e) => Applicative (ReporterT e f) where
    pure x = ReporterT (pure (pure x))
    -- apply monoidally merges errors and applies values
    ReporterT f <*> ReporterT x =
        ReporterT (liftA2 (<*>) f x)

report1 :: e -> Accumulate [e] a
report1 msg = ALeft [msg]

reportMany errs = ALeft errs

runReporterT :: Applicative m => ReporterT e m a -> m (e | a)
runReporterT reporter = fmap toEither reporter.run

